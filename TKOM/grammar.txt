program             : { function }
                    ;

function            : ( "void" | type ) IDENTIFIER "(" [paramList] ")" block
                    ;

type                : "int"
                    ;

paramList           : { type IDENTIFIER, } type IDENTIFIER
                    ;

block               : "{" { instruction } "}"
                    ;

const               : INT_CONST
                    ;

instruction         : simple_instruction ";"
                    | block_instruction
                    ;
simple_instruction  : declaration
                    | read
                    | print
                    | return
                    | assignment
                    | throw
                    ;
block_instruction   : block
                    | if
                    | while
                    | try_catch_finally
                    ;

declaration         : type { declOptAssign "," } declOptAssign
                    ;
declOptAssign       : IDENTIFIER [ "=" const ]
                    ;

read                : "read" IDENTIFIER
                    | "read" "(" IDENTIFIER ")"
                    ;

print               : "print" ( string | statement )
                    | "print" "(" ( string | statement ) ")"
                    ;

string              : STRING { "+" ( STRING | const ) }
                    ;

return              : "return" statement
                    ;

if                  : "if" "(" statement ")" instruction [ "else" instruction ]
                    ;

while               : "while" "(" statement ")" instruction
                    ;

// ================= EXCEPTIONS =================
throw               : "throw" "Exception" "(" INT_CONST ")"
                    ;

try_catch_finally   : "try" block 
                        catch { catch }
                        [ "finally" block ]
                    ;
catch               : "catch" "Exception" IDENTIFIER [ "when" "(" statement ")" ] block
                    ;

// ================= OPERATORS =================
statement           : assignment
                    | logicalOr
                    ;

assignment          : lvalue "=" statement
                    ;

logicalOr           : logicalOr "||" logicalAnd
                    | logicalAnd
                    ;

logicalAnd          : logicalAnd "&&" bitOr
                    | bitOr
                    ;

bitOr               : bitOr "|" bitXor
                    | bitXor
                    ;

bitXor              : bitXor "^" bitAnd
                    | bitAnd
                    ;

BitAnd              : BitAnd "&" in_equality
                    : in_equality
                    ;

in_equality         : in_equality "==" relation
                    | in_equality "!=" relation
                    | relation
                    ;

relation            : relation "<=" additive
                    | relation ">=" additive
                    | relation "<" additive
                    | relation ">" additive
                    | additive
                    ;

additive            : additive "+" multiplicative
                    | additive "-" multiplicative
                    | multiplicative
                    ;

multiplicative      : multiplicative "*" unar
                    | multiplicative "/" unar
                    | unar
                    ;

unar                : "-" unar
                    | "~" unar
                    | "!" unar
                    | const
                    | lvalue
                    | "(" statement ")"
                    ;

lvalue              : { IDENTIFIER "." } IDENTIFIER
                    ;


int fun(int x)
{
    try {
        int A = x / 0;
    } catch Exception ex when (5 == 5) {
        print "\tups!\\\n" + 10;
    } finally {
        return x + 1;
    }
}

void main()
{
    int a, b = 17;
    a = 10;
    int i, j = -89, k;
    j = j + b*2/a - 3;

    read(i);
    if (i < j + !~a && 1 == 1)
        print fun(3);
    else
        print ":)";

    return;
}
