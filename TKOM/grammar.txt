program             : { function }
                    ;

function            : ( "void" | type ) IDENTIFIER param_list block
                    ;

type                : "int"
                    ;

param_list          : "(" [ type IDENTIFIER { "," type IDENTIFIER } ] ")"
                    ;

block               : "{" { statement } "}"
                    ;

const               : INT_CONST
                    ;

statement           : simple_statement ";"
                    | block_statement
                    ;
simple_statement    : declaration
                    | assignment
                    | function_call
                    | return
                    | throw
                    ;
block_statement     : if
                    | while
                    | try_catch_finally
                    ;

declaration         : type declOptAssign { "," decl_opt_assign }
                    ;
decl_opt_assign     : IDENTIFIER [ "=" const ]
                    ;

function_call       : IDENTIFIER "(" [ expression { "," expression } ] ")"
                    ;

string              : STRING { "+" ( STRING | const ) }
                    ;

return              : "return" [ expression ]
                    ;

if                  : "if" "(" expression ")" statement [ "else" statement ]
                    ;

while               : "while" "(" expression ")" statement
                    ;

// ================= EXCEPTIONS =================
throw               : "throw" "Exception" "(" expression ")"
                    ;

try_catch_finally   : "try" statement 
                        catch { catch }
                        [ "finally" statement ]
                    ;
catch               : "catch" "Exception" IDENTIFIER [ "when" expression ] statement
                    ;

// ================= OPERATORS =================
expression          : assignment
                    | logical_or
                    ;

assignment          : lvalue "=" expression
                    ;

logical_or          : logical_and { "||" logical_and }
                    ;

logical_and         : in_equality { "&&" in_equality }
                    ;

in_equality         : relation _in_equality
                    ;
_in_equality        : "==" relation _in_equality
                    | "!=" relation _in_equality
                    | e
                    ;

relation            : additive _relation
                    ;
_relation           : "<=" additive _relation
                    | ">=" additive _relation
                    | "<" additive _relation
                    | ">" additive _relation
                    | e
                    ;

additive            : multiplicative _additive
                    ;
_additive           : "+" multiplicative _additive
                    | "-" multiplicative _additive
                    | e
                    ;

multiplicative      : unar _multiplicative
                    ;
_multiplicative     : "*" unar _multiplicative
                    | "/" unar _multiplicative
                    | e
                    ;

unar                : "-" unar
                    | "+" unar
                    | "!" unar
                    | const
                    | lvalue
                    | function_call
                    | string
                    | "(" expression ")"
                    ;

lvalue              : IDENTIFIER { "." IDENTIFIER }
                    ;
